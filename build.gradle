buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT"
    }
}

apply plugin: "idea"

idea {
    module {
        // For some reason this is necessary for IDEA 2014 workspaces to recognize assets
        inheritOutputDirs = true
    }
}

apply plugin: "net.minecraftforge.gradle.forge"

repositories {
    maven {
        // location of the maven that hosts JEI files
        name = "Progwml6 maven"
        url = "http://dvs1.progwml6.com/files/maven"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "modmaven.k-4u.nl"
    }
}

// Define properties file
ext.configFile = file "build.properties"

configFile.withReader {
    // Load config. It shall from now be referenced as simply config or project.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

version = config.minecraft_version + "-" + config.mod_version
group = "org.halvors.nuclearphysics"
archivesBaseName = "nuclearphysics"
sourceCompatibility = "1.8"
targetCompatibility = "1.8"

minecraft {
	version = config.minecraft_version + "-" + config.forge_version
    runDir = "eclipse"
    mappings = config.mcp_mappings

    // Replace version in Reference.java
    replace "@version", config.mod_version
}

dependencies {
    // compile against the JEI API but do not include it at runtime
    deobfProvided "mezz.jei:jei_${config.minecraft_version}:${config.jei_version}:api"
    // at runtime, use the full JEI jar
    runtime "mezz.jei:jei_${config.minecraft_version}:${config.jei_version}"
}

processResources {
    // This will ensure that this task is redone when this versions change.
    inputs.property "mod_version", config.mod_version
    inputs.property "minecraft_version", config.minecraft_version
    inputs.property "forge_version", config.forge_version

    // Replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // Replace version and minecraft_version
        expand "version" : config.mod_version,
               "minecraft_version" : config.minecraft_version
    }

    // Copy everything else
    from(sourceSets.main.resources.srcDirs) {
		exclude "mcmod.info"
    }
}